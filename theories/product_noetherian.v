(**************************************************************)
(*      This file is distributed under the terms of the       *)
(*        Mozilla Public License Version 2.0, MPL-2.0         *)
(**************************************************************)

From Stdlib Require Import List Ring Setoid Utf8.

Require Import utils bar ring product category ideal noetherian.

Import ListNotations.

(** This proof was inspired by a Rocq rework of the proof
    of the constructive form of Ramsey's theorem 

     [1] "Higman's lemma in Type theory", D. Fridlender
            in TYPES 1996

   which is expressed there as "the direct product of two
   almost full binary relations is almost full"

   See file ramsey.v herein for the Rocq rework of that
   proof in [1]. *)

#[local] Notation PA := pauses.

Section product_noetherian.

  Variables (ğ“¡ ğ“£ : ring).
 
  Add Ring ğ“¡_is_ring : (is_ring ğ“¡).
  Add Ring ğ“£_is_ring : (is_ring ğ“£).

  Let ğ“Ÿ := product_ring ğ“¡ ğ“£.

  Implicit Types (lx : list ğ“¡) (ly : list ğ“£) (l : list ğ“Ÿ).

  Add Ring ğ“Ÿ_is_ring : (is_ring ğ“Ÿ).

  Let Ï† (x : ğ“¡) : ğ“Ÿ := (x,0áµ£).
  Let Ïˆ (y : ğ“£) : ğ“Ÿ := (0áµ£,y).

  Let Ï€â‚ (z : ğ“Ÿ) : ğ“¡ := fst z.
  Let Ï€â‚‚ (z : ğ“Ÿ) : ğ“£ := snd z.

  (** Mostly obvious observations about Ï€â‚, Ï€â‚‚, Ï† and Ïˆ *)

  Local Fact Ï†_sub_homo : ring_sub_homo Ï†.
  Proof. split right; simpl; ring || split; (auto || ring). Qed.

  Local Fact Ïˆ_sub_homo : ring_sub_homo Ïˆ.
  Proof. split right; simpl; ring || split; (auto || ring). Qed.

  Local Fact Ï€â‚_sub_homo : ring_sub_homo Ï€â‚.
  Proof. split right; simpl; ring || auto || tauto. Qed.

  Local Fact Ï€â‚‚_sub_homo : ring_sub_homo Ï€â‚‚.
  Proof. split right; simpl; ring || auto || tauto. Qed.

  (* May be the least trivial observation, by induction on l *)
  Local Lemma idl_Ï€â‚_Ï€â‚‚ l x y : idl âŒmap Ï€â‚ lâŒŸ x â†’ idl âŒmap Ï€â‚‚ lâŒŸ y â†’ idl âŒlâŒŸ (x,y).
  Proof.
    rewrite !idl_iff_lc__list.
    induction l as [ | (u,v) l IHl ] in x, y |- *; simpl.
    + intros ?%lc_inv ?%lc_inv; constructor; split; auto.
    + intros (a & u' & H1 & H2)%lc_inv (b & v' & H3 & H4)%lc_inv.
      specialize (IHl _ _ H1 H3).
      constructor 2 with (a,b) (u',v'); auto.
      simpl; split; auto.
  Qed.

  Hint Resolve in_map : core.

  (* Ï† (Ï€â‚ z) = (1áµ£,0áµ£) *áµ£ z *)
  Local Fact idl_Ï† l z : idl âŒlâŒŸ z â†’ idl âŒlâŒŸ (Ï† (Ï€â‚ z)).
  Proof.
    unfold Ï€â‚.
    constructor 2 with (x := ((1áµ£,0áµ£) : ğ“Ÿ) *áµ£ z); auto.
    split; simpl; ring.
  Qed.

  Hint Resolve in_or_app in_eq in_cons : core.

  Local Corollary idl_Ï†_Ï€â‚ l z r : idl âŒl++Ï† (Ï€â‚ z)::râŒŸ âŠ†â‚ idl âŒl++z::râŒŸ.
  Proof.
    apply idl_closed.
    intros ? [ | [ <- | ] ]%in_app_iff.
    2: apply idl_Ï†.
    all: constructor 1; eauto.
  Qed.

  (* Ïˆ (Ï€â‚‚ z) = (0áµ£,1áµ£) *áµ£ z *)
  Local Fact idl_Ïˆ l z : idl âŒlâŒŸ z â†’ idl âŒlâŒŸ (Ïˆ (Ï€â‚‚ z)).
  Proof.
    unfold Ï€â‚‚.
    constructor 2 with (x := ((0áµ£,1áµ£) : ğ“Ÿ) *áµ£ z); auto.
    split; simpl; ring.
  Qed.

  Local Corollary idl_Ïˆ_Ï€â‚‚ l z r : idl âŒl++Ïˆ (Ï€â‚‚ z)::râŒŸ âŠ†â‚ idl âŒl++z::râŒŸ.
  Proof.
    apply idl_closed.
    intros ? [ | [ <- | ] ]%in_app_iff.
    2: apply idl_Ïˆ.
    all: constructor 1; eauto.
  Qed.

  Local Fact idl_Ï†_iff l x : idl âŒmap Ï€â‚ lâŒŸ x â†” idl âŒlâŒŸ (Ï† x).
  Proof.
    split.
    + intro; apply idl_Ï€â‚_Ï€â‚‚; auto.
    + intros H.
      apply idl_sub_homo with (1 := Ï€â‚_sub_homo) in H.
      revert H; simpl; apply idl_mono.
      intros ? (? & -> & ?); auto.
  Qed.

  Local Corollary idl_Ïˆ_iff l y : idl âŒmap Ï€â‚‚ lâŒŸ y â†” idl âŒlâŒŸ (Ïˆ y).
  Proof.
    split.
    + intro; apply idl_Ï€â‚_Ï€â‚‚; auto.
    + intros H.
      apply idl_sub_homo with (1 := Ï€â‚‚_sub_homo) in H.
      revert H; simpl; apply idl_mono.
      intros ? (? & -> & ?); auto.
  Qed.

  Local Corollary idl_Ï†_Ïˆ l : âˆ€z, idl âŒlâŒŸ (Ï† (Ï€â‚ z)) â†’ idl âŒlâŒŸ (Ïˆ (Ï€â‚‚ z)) â†’ idl âŒlâŒŸ z.
  Proof. intros [] ?%idl_Ï†_iff ?%idl_Ïˆ_iff; now apply idl_Ï€â‚_Ï€â‚‚. Qed.

  Hint Resolve idl_Ï† idl_Ïˆ : core.

  Local Remark idl_Ï†_Ïˆ_iff l x y : idl âŒlâŒŸ (x,y) â†” idl âŒlâŒŸ (Ï† x) âˆ§ idl âŒlâŒŸ (Ïˆ y).
  Proof.
    change y with (snd (x,y)) at 2.
    change x with (fst (x,y)) at 2.
    generalize (x,y).
    split; eauto.
    intros []; now apply idl_Ï†_Ïˆ.
  Qed.

  (** Now comes the non-trivial aspect of this proof:

      we define the "critical" parameterized over-approximation of PA
      which needed to be adapted from the proof of the constructive
      form of Ramsey's theorem in [1] (see ramsey.v):

      Î¸ lx ly over-approximates PA, and matches PA when lx = ly = [] 

      Notice that in ramsey.v (hence for relations, not ideals), 
      Î¸ lx ly is defined as (equivalent to)

           good RS (map inXY l++map inX lx++map inY ly)

      where RS an "obvious" extension of R and S on the type X*Y+X+Y
      (see Î¸_iff_good in ramsey.v).

      This equivalent form "is not" made explicit in [1] and is 
      rather obfuscated by overly complex notations and unnecessary
      auxiliary functions and hypotheses. Getting this form, as an
      instance of the "good" predicate, was essential be able to 
      convert the over approximation from relations to ideals.

      Indeed, as good    := MC (Î» l x, âˆƒy, y âˆˆ l âˆ§ R y x)
              and pauses := MC (Î» l x, idl âŒlâŒŸ x), 
      we get a clear similarity here but we inject ğ“¡ (resp. ğ“£) 
      into ğ“¡*ğ“£ using Ï† (resp. Ïˆ) instead of the canonical injections 
      X â†’ X*Y+X+Y and Y â†’ X*Y.X+Y. *)

  Let Î¸ lx ly l := PA (l++map Ï† lx++map Ïˆ ly).

  Local Fact Î¸_monotone lx ly : monotone (Î¸ lx ly).
  Proof. intros ? ? ?; now apply PA_monotone. Qed.

  Local Fact bar_Î¸_monotone lx ly : monotone (bar (Î¸ lx ly)).
  Proof. apply bar_monotone, Î¸_monotone. Qed.

  Local Proposition bar_Î¸_nil_nil_PA : bar (Î¸ [] []) âŠ†â‚ bar PA.
  Proof. apply bar_mono; intro; unfold Î¸; simpl; now rewrite app_nil_r. Qed.

  Section Ramsey_nested_induction.

    (** This part, with nested induction, largely differs for
        the corresponding one in ramsey.v, and is in fact
        simpler to obtain (IMHO), once you understand that
        you first have the consider the difficult base case 
        in bar_bar_ramsey. *)

    Hint Resolve in_or_app in_eq in_cons : core.

    Variables (lx : list ğ“¡) (ly : list ğ“£) (z : ğ“¡*ğ“£).

    (** First observation: when Ï€â‚‚ z is in the
        ideal generated by ly, then we can deal with
        the case where the pause in

            m++[Ï† (Ï€â‚ z)]++map Ï† lx++map Ïˆ ly

        occurs at Ï† (Ï€â‚ z), the other cases being
        trivial. *)

    Local Proposition idl_Î¸_ramsey m :
        idl âŒlyâŒŸ (Ï€â‚‚ z)
      â†’ Î¸ (Ï€â‚ z::lx) ly m
      â†’ Î¸ lx ly (m++[z]).
    Proof.
      unfold Î¸; simpl; rewrite <- !app_assoc.
      intros Hz [ (l & u & r & -> & Hu) | [] ]%PA_middle_inv.
      + (* The pause occurs inside m *)
        rewrite <- app_assoc.
        apply PA_app_left.
        simpl; constructor 1.
        now apply idl_Ï†_Ï€â‚ in Hu.
      + (* The pause occurs at Ï† (Ï€â‚ z) *)
        apply PA_app_left.
        simpl; constructor 1.
        apply idl_Ï†_Ïˆ; auto.
        apply idl_Ïˆ_iff.
        rewrite map_app, !map_map; simpl; rewrite map_id.
        revert Hz; apply idl_mono; eauto.
      + (* The pause occurs inside map Ï† lx++map Ïˆ ly *)
        now do 2 apply PA_app_left.
    Qed.

    Local Corollary idl_bar_ramsey m :
        idl âŒlyâŒŸ (Ï€â‚‚ z)
      â†’ bar (Î¸ (Ï€â‚ z::lx) ly) m
      â†’ bar (Î¸ lx ly) (m++[z]).
    Proof.
      intros Hz Hm.
      apply bar_app_iff.
      revert m Hm; apply bar_mono.
      intro; now apply idl_Î¸_ramsey.
    Qed.

    (** Now we can proceed by induction on 
          bar (Î¸ lx (Ï€â‚‚ z::ly)) m 
        and the difficulty lies only
        in the base case when the pause
        occurs at (Ï€â‚‚ z), and we use
        idl_bar_ramsey *)
 
    Local Proposition bar_bar_ramsey m :
        bar (Î¸ lx (Ï€â‚‚ z::ly)) m
      â†’ bar (Î¸ (Ï€â‚ z::lx) ly) m
      â†’ bar (Î¸ lx ly) (m++[z]).
    Proof.
      induction 1 as [ m Hm | m _ IHm ].
      + red in Hm; simpl in Hm.
        apply PA_special_inv in Hm
          as [ (l & u & r & -> & Hu) 
           | [ (l' & v & r' & ? & Hv)
           | [ Hm | Hm ] ] ].
        * (* the pause occurs inside m *)
          intros _. (* no nested recursion needed *)
          constructor 1; red; simpl.
          repeat (rewrite <- !app_assoc; simpl).
          apply PA_app_left; constructor 1.
          rewrite app_assoc in Hu; apply idl_Ïˆ_Ï€â‚‚ in Hu.
          revert u Hu; apply idl_mono.
          intro; simpl; repeat (rewrite !in_app_iff; simpl); tauto.
        * (* the pause occurs in map Ï† lx *)
          intros _. (* no nested recursion needed *)
          constructor 1; red; simpl.
          apply PA_app_left.
          apply map_split_inv in H as (l & u & r & -> & <- & <- & <-).
          rewrite map_app; simpl; rewrite <- app_assoc; simpl.
          apply PA_app_left; constructor 1.
          rewrite <- idl_Ï†_iff in Hv |- *.
          revert u Hv.
          rewrite !map_app; simpl; rewrite !map_map; simpl; rewrite !map_id.
          apply idl_closed; intros ? [ | [ <- | ] ]%in_app_iff; eauto.
        * (* the pause occurs at Ï€â‚‚ z *)
          apply idl_bar_ramsey. (* we use nested recursion *)
          apply idl_Ïˆ_iff in Hm; revert Hm.
          rewrite map_map; simpl; now rewrite map_id.
        * (* the pause occurs in map Ïˆ ly *)
          intros _. (* no nested recursion needed *)
          constructor 1; red; simpl.
          now do 2 apply PA_app_left.
      + (* directly using the induction hypothesis,
           provided that bar (Î¸ _ _) is monotone *)
        constructor 2; intro; apply IHm.
        now apply bar_Î¸_monotone.
    Qed.

    Hypothesis (B1 : bar (Î¸ (Ï€â‚ z::lx) ly) []).
    Hypothesis (B2 : bar (Î¸ lx (Ï€â‚‚ z::ly)) []).

    Local Proposition bar_ramsey : bar (Î¸ lx ly) [z].
    Proof. apply bar_bar_ramsey with (m := []); auto. Qed.

  End Ramsey_nested_induction.

  Hint Resolve Ï†_sub_homo Ïˆ_sub_homo : core.

  (* This proceeds by simultaneous induction on bar PA lx and bar PA ly,
     and the proof sketch is nearly the same as in [1], see file 
     ramsey.v herein *)
  Local Lemma bar_PA__bar_Î¸ lx ly : bar PA lx â†’ bar PA ly â†’ bar (Î¸ lx ly) [].
  Proof.
    double bar induction as Hlx Hly.
    + constructor 1; red; simpl.
      apply PA_app_right.
      revert Hlx; apply PA_sub_homo; auto.
    + constructor 1; red; simpl.
      apply PA_app_left.
      revert Hly; apply PA_sub_homo; auto.
    + constructor 2; intro; apply bar_ramsey; auto.
  Qed.

  Theorem product_noetherian : noetherian ğ“¡ â†’ noetherian ğ“£ â†’ noetherian ğ“Ÿ.
  Proof. intros; now apply bar_Î¸_nil_nil_PA, bar_PA__bar_Î¸. Qed.

End product_noetherian.

Check product_ring.
Check product_ring_correct.
Print is_product_ring.
Check product_noetherian.


  


